{
  "language": "Solidity",
  "sources": {
    "contracts/IRealitio.sol": {
      "content": "// SPDX-License-Identifier: MIT\r\n\r\n/**\r\n *  @authors: [@ferittuncer]\r\n *  @reviewers: [@hbarcelos]\r\n *  @auditors: []\r\n *  @bounties: []\r\n *  @deployments: []\r\n */\r\n\r\npragma solidity ^0.7.6;\r\n\r\n/**\r\n *  @title IRealitio\r\n *  @dev Required subset of https://github.com/realitio/realitio-contracts/blob/master/truffle/contracts/IRealitio.sol to implement a Realitio arbitrator.\r\n */\r\ninterface IRealitio {\r\n    /// @notice Notify the contract that the arbitrator has been paid for a question, freezing it pending their decision.\r\n    /// @dev The arbitrator contract is trusted to only call this if they've been paid, and tell us who paid them.\r\n    /// @param question_id The ID of the question\r\n    /// @param requester The account that requested arbitration\r\n    /// @param max_previous If specified, reverts if a bond higher than this was submitted after you sent your transaction.\r\n    function notifyOfArbitrationRequest(\r\n        bytes32 question_id,\r\n        address requester,\r\n        uint256 max_previous\r\n    ) external;\r\n\r\n    /// @notice Submit the answer for a question, for use by the arbitrator.\r\n    /// @dev Doesn't require (or allow) a bond. Required only in v2.0.\r\n    /// If the current final answer is correct, the account should be whoever submitted it.\r\n    /// If the current final answer is wrong, the account should be whoever paid for arbitration.\r\n    /// However, the answerer stipulations are not enforced by the contract.\r\n    /// @param question_id The ID of the question.\r\n    /// @param answer The answer, encoded into bytes32.\r\n    /// @param answerer The account credited with this answer for the purpose of bond claims.\r\n    function submitAnswerByArbitrator(\r\n        bytes32 question_id,\r\n        bytes32 answer,\r\n        address answerer\r\n    ) external;\r\n\r\n    /// @notice Returns the history hash of the question. Required before calling submitAnswerByArbitrator to make sure history is correct.\r\n    /// @dev Required only in v2.0.\r\n    /// @param question_id The ID of the question.\r\n    /// @dev Updated on each answer, then rewound as each is claimed.\r\n    function getHistoryHash(bytes32 question_id) external view returns (bytes32);\r\n\r\n    /// @notice Returns the commitment info by its id. Required before calling submitAnswerByArbitrator to make sure history is correct.\r\n    /// @dev Required only in v2.0.\r\n    /// @param commitment_id The ID of the commitment.\r\n    /// @return Time after which the committed answer can be revealed.\r\n    /// @return Whether the commitment has already been revealed or not.\r\n    /// @return The committed answer, encoded as bytes32.\r\n    function commitments(bytes32 commitment_id)\r\n        external\r\n        view\r\n        returns (\r\n            uint32,\r\n            bool,\r\n            bytes32\r\n        );\r\n\r\n    /// @notice Submit the answer for a question, for use by the arbitrator, working out the appropriate winner based on the last answer details.\r\n    /// @dev Doesn't require (or allow) a bond. Required only in v2.1.\r\n    /// @param question_id The ID of the question\r\n    /// @param answer The answer, encoded into bytes32\r\n    /// @param payee_if_wrong The account to by credited as winner if the last answer given is wrong, usually the account that paid the arbitrator\r\n    /// @param last_history_hash The history hash before the final one\r\n    /// @param last_answer_or_commitment_id The last answer given, or the commitment ID if it was a commitment.\r\n    /// @param last_answerer The address that supplied the last answer\r\n    function assignWinnerAndSubmitAnswerByArbitrator(\r\n        bytes32 question_id,\r\n        bytes32 answer,\r\n        address payee_if_wrong,\r\n        bytes32 last_history_hash,\r\n        bytes32 last_answer_or_commitment_id,\r\n        address last_answerer\r\n    ) external;\r\n}\r\n"
    },
    "contracts/Realitio_v2_1_ArbitratorWithAppeals.sol": {
      "content": "// SPDX-License-Identifier: MIT\r\n\r\n/**\r\n *  @authors: [@ferittuncer]\r\n *  @reviewers: [@unknownunknown1*, @hbarcelos, @MerlinEgalite, @shalzz, @fnanni-0, @clesaege, @jaybuidl]\r\n *  @auditors: []\r\n *  @bounties: []\r\n *  @deployments: []\r\n */\r\n\r\npragma solidity ^0.7.0;\r\n\r\nimport \"./IRealitio.sol\";\r\nimport \"./RealitioArbitratorWithAppealsBase.sol\";\r\n\r\n/**\r\n *  @title Realitio_v2_1_ArbitratorWithAppeals\r\n *  @dev A Realitio arbitrator implementation that uses Realitio v2.1 and Kleros. It notifies Realitio contract for arbitration requests and creates corresponding dispute on Kleros.\r\n *  Transmits Kleros ruling to Realitio contract. Maintains crowdfunded appeals and notifies Kleros contract. Provides a function to submit evidence for Kleros dispute.\r\n *  There is a conversion between Kleros ruling and Realitio answer and there is a need for shifting by 1. This is because ruling 0 in Kleros signals tie or no-ruling but in Realitio 0 is a valid answer.\r\n *  For reviewers this should be a focus as it's quite easy to get confused. Any mistakes on this conversion will render this contract useless.\r\n *  NOTE: This contract trusts the Kleros arbitrator and Realitio.\r\n */\r\ncontract Realitio_v2_1_ArbitratorWithAppeals is RealitioArbitratorWithAppealsBase {\r\n    /** @dev Constructor.\r\n     *  @param _realitio The address of the Realitio contract.\r\n     *  @param _metadata The metadata required for RealitioArbitrator.\r\n     *  @param _arbitrator The address of the ERC792 arbitrator.\r\n     *  @param _arbitratorExtraData The extra data used to raise a dispute in the ERC792 arbitrator.\r\n     *  @param _metaevidence Metaevidence as defined in ERC-1497.\r\n     */\r\n    constructor(\r\n        IRealitio _realitio,\r\n        string memory _metadata,\r\n        IArbitrator _arbitrator,\r\n        bytes memory _arbitratorExtraData,\r\n        string memory _metaevidence\r\n    ) RealitioArbitratorWithAppealsBase(_realitio, _metadata, _arbitrator, _arbitratorExtraData, _metaevidence) {}\r\n\r\n    /** @dev Reports the answer to a specified question from the Kleros arbitrator to the Realitio v2.1 contract.\r\n     *  This can be called by anyone, after the dispute gets a ruling from Kleros.\r\n        We can't directly call `assignWinnerAndSubmitAnswerByArbitrator` inside `rule` because of last answerer is not stored on chain.\r\n     *  @param _questionID The ID of Realitio question.\r\n     *  @param _lastHistoryHash The history hash given with the last answer to the question in the Realitio contract.\r\n     *  @param _lastAnswerOrCommitmentID The last answer given, or its commitment ID if it was a commitment, to the question in the Realitio contract, in bytes32.\r\n     *  @param _lastAnswerer The last answerer to the question in the Realitio contract.\r\n     */\r\n    function reportAnswer(\r\n        bytes32 _questionID,\r\n        bytes32 _lastHistoryHash,\r\n        bytes32 _lastAnswerOrCommitmentID,\r\n        address _lastAnswerer\r\n    ) external {\r\n        ArbitrationRequest storage arbitrationRequest = arbitrationRequests[uint256(_questionID)];\r\n        require(arbitrationRequest.status == Status.Ruled, \"The status should be Ruled.\");\r\n\r\n        arbitrationRequest.status = Status.Reported;\r\n\r\n        // Note that ruling is shifted by -1 before calling Realitio. This works because 0-1 is equivalent to type(uint256).max. However, this won't be the case starting from Solidity 0.8.x.\r\n        // https://docs.soliditylang.org/en/v0.8.0/080-breaking-changes.html\r\n        realitio.assignWinnerAndSubmitAnswerByArbitrator(_questionID, bytes32(arbitrationRequest.ruling - 1), arbitrationRequest.requester, _lastHistoryHash, _lastAnswerOrCommitmentID, _lastAnswerer);\r\n    }\r\n}\r\n"
    },
    "contracts/RealitioArbitratorWithAppealsBase.sol": {
      "content": "// SPDX-License-Identifier: MIT\r\n\r\n/**\r\n *  @authors: [@ferittuncer]\r\n *  @reviewers: [@unknownunknown1, @hbarcelos*, @MerlinEgalite*, @shalzz, @fnanni-0*, @clesaege*, @jaybuidl, @orlandothefraser]\r\n *  @auditors: []\r\n *  @bounties: []\r\n */\r\n\r\npragma solidity ^0.7.0;\r\n\r\nimport \"./IRealitio.sol\";\r\nimport \"./IRealitioArbitrator.sol\";\r\nimport \"@kleros/dispute-resolver-interface-contract/contracts/solc-0.7.x/IDisputeResolver.sol\";\r\nimport \"@kleros/ethereum-libraries/contracts/CappedMath.sol\";\r\n\r\n/**\r\n *  @title RealitioArbitratorWithAppealsBase\r\n *  @dev A Realitio arbitrator base implementation that uses Realitio v2.x and Kleros. It notifies Realitio contract for arbitration requests and creates corresponding dispute on Kleros. Transmits Kleros ruling to Realitio contract. Maintains crowdfunded appeals and notifies Kleros contract. Provides a function to submit evidence for Kleros dispute. This contract is abstract as it does not have a function to report answer, see child contracts.\r\n *  There is a conversion between Kleros ruling and Realitio answer and there is a need for shifting by 1. This is because ruling 0 in Kleros signals tie or no-ruling but in Realitio 0 is a valid answer. For reviewers this should be a focus as it's quite easy to get confused. Any mistakes on this conversion will render this contract useless.\r\n *  NOTE: This contract trusts the Kleros arbitrator and Realitio.\r\n */\r\nabstract contract RealitioArbitratorWithAppealsBase is IDisputeResolver, IRealitioArbitrator {\r\n    using CappedMath for uint256; // Overflows and underflows are prevented by returning uint256 max and min values in case of overflows and underflows, respectively.\r\n\r\n    IRealitio public immutable override realitio; // Actual implementation of Realitio.\r\n    IArbitrator public immutable arbitrator; // The Kleros arbitrator.\r\n    bytes public arbitratorExtraData; // Required for Kleros arbitrator. First 64 bytes contain subcourtID and the second 64 bytes contain number of votes in the jury.\r\n    string public override metadata; // Metadata for Realitio. See IRealitioArbitrator.\r\n\r\n    // The required fee stake that a party must deposit, which depends on who won the previous round and is proportional to the arbitration cost such that the fee stake for a round is `multiplier * arbitration_cost` for that round.\r\n    uint256 public constant WINNER_STAKE_MULTIPLIER = 3000; // Multiplier of the arbitration cost that the winner has to pay as fee stake for a round in basis points.\r\n    uint256 public constant LOSER_STAKE_MULTIPLIER = 7000; // Multiplier of the arbitration cost that the loser has to pay as fee stake for a round in basis points.\r\n    uint256 public constant LOSER_APPEAL_PERIOD_MULTIPLIER = 5000; // Multiplier of the appeal period for losers (any other ruling options) in basis points. The loser is given less time to fund its appeal to defend against last minute appeal funding attacks.\r\n    uint256 public constant MULTIPLIER_DENOMINATOR = 10000; // Denominator for multipliers.\r\n    uint256 private constant NUMBER_OF_RULING_OPTIONS = type(uint256).max; // The amount of non 0 choices the arbitrator can give.\r\n\r\n    enum Status {\r\n        None, // The question hasn't been requested arbitration yet.\r\n        Disputed, // The question has been requested arbitration.\r\n        Ruled, // The question has been ruled by arbitrator.\r\n        Reported // The answer of the question has been reported to Realitio.\r\n    }\r\n\r\n    // To track internal dispute state in this contract.\r\n    struct ArbitrationRequest {\r\n        Status status; // The current status of the question.\r\n        address requester; // The address that requested the arbitration.\r\n        uint256 disputeID; // The ID of the dispute raised in the arbitrator contract.\r\n        uint256 ruling; // The ruling given by the arbitrator.\r\n        Round[] rounds; // Tracks each appeal round of a dispute.\r\n    }\r\n\r\n    // For appeal logic.\r\n    struct Round {\r\n        mapping(uint256 => uint256) paidFees; // Tracks the fees paid in this round in the form paidFees[answer].\r\n        mapping(uint256 => bool) hasPaid; // True if the fees for this particular answer has been fully paid in the form hasPaid[answer].\r\n        mapping(address => mapping(uint256 => uint256)) contributions; // Maps contributors to their contributions for each answer in the form contributions[address][answer].\r\n        uint256 feeRewards; // Sum of reimbursable appeal fees available to the parties that made contributions to the answer that ultimately wins a dispute.\r\n        uint256[] fundedRulings; // Stores the answer choices that are fully funded.\r\n    }\r\n\r\n    mapping(uint256 => ArbitrationRequest) public arbitrationRequests; // Maps a question identifier in uint to its arbitration details. Example: arbitrationRequests[uint(questionID)]\r\n    mapping(uint256 => uint256) public override externalIDtoLocalID; // Map arbitrator dispute identifiers to local identifiers. We use question ids casted to uint as local identifier.\r\n\r\n    /** @dev Emitted when arbitration is requested, to link dispute identifier to question identifier for dynamic script that is used in metaevidence. See https://github.com/kleros/realitio-script/blob/master/src/index.js\r\n     *  @param _disputeID The ID of the dispute in the ERC792 arbitrator.\r\n     *  @param _questionID The ID of the question.\r\n     */\r\n    event DisputeIDToQuestionID(uint256 indexed _disputeID, bytes32 _questionID);\r\n\r\n    /** @dev Constructor.\r\n     *  @param _realitio The address of the Realitio contract.\r\n     *  @param _metadata The metadata required for RealitioArbitrator.\r\n     *  @param _arbitrator The address of the ERC792 arbitrator.\r\n     *  @param _arbitratorExtraData The extra data used to raise a dispute in the ERC792 arbitrator.\r\n     *  @param _metaevidence Metaevidence as defined in ERC-1497.\r\n     */\r\n    constructor(\r\n        IRealitio _realitio,\r\n        string memory _metadata,\r\n        IArbitrator _arbitrator,\r\n        bytes memory _arbitratorExtraData,\r\n        string memory _metaevidence\r\n    ) {\r\n        realitio = _realitio;\r\n        metadata = _metadata;\r\n        arbitrator = _arbitrator;\r\n        arbitratorExtraData = _arbitratorExtraData;\r\n        emit MetaEvidence(0, _metaevidence); // No setter for meta-evidence. To change it, deploy a new contract.\r\n    }\r\n\r\n    /** @dev Allows to submit evidence for a given dispute.\r\n     *  @param _questionID Realitio question identifier.\r\n     *  @param _evidenceURI Link to evidence.\r\n     */\r\n    function submitEvidence(uint256 _questionID, string calldata _evidenceURI) external override {\r\n        emit Evidence(arbitrator, _questionID, msg.sender, _evidenceURI); // We use _questionID for evidence group identifier.\r\n    }\r\n\r\n    /** @dev Request arbitration from Kleros for given _questionID.\r\n     *  @param _questionID The question identifier in Realitio contract.\r\n     *  @param _maxPrevious If specified, reverts if a bond higher than this was submitted after you sent your transaction.\r\n     *  @return disputeID ID of the resulting dispute on arbitrator.\r\n     */\r\n    function requestArbitration(bytes32 _questionID, uint256 _maxPrevious) external payable returns (uint256 disputeID) {\r\n        ArbitrationRequest storage arbitrationRequest = arbitrationRequests[uint256(_questionID)];\r\n        require(arbitrationRequest.status == Status.None, \"Arbitration already requested\");\r\n\r\n        // Notify Kleros\r\n        disputeID = arbitrator.createDispute{value: msg.value}(NUMBER_OF_RULING_OPTIONS, arbitratorExtraData); /* If msg.value is greater than intended number of votes (specified in arbitratorExtraData),\r\n        Kleros will automatically spend excess for additional votes. */\r\n        emit Dispute(arbitrator, disputeID, 0, uint256(_questionID)); // We use _questionID in uint as evidence group identifier.\r\n        emit DisputeIDToQuestionID(disputeID, _questionID); // For the dynamic script https://github.com/kleros/realitio-script/blob/master/src/index.js\r\n        externalIDtoLocalID[disputeID] = uint256(_questionID);\r\n\r\n        // Update internal state\r\n        arbitrationRequest.requester = msg.sender;\r\n        arbitrationRequest.status = Status.Disputed;\r\n        arbitrationRequest.disputeID = disputeID;\r\n        arbitrationRequest.rounds.push();\r\n\r\n        // Notify Realitio\r\n        realitio.notifyOfArbitrationRequest(_questionID, msg.sender, _maxPrevious);\r\n    }\r\n\r\n    /** @dev Receives ruling from Kleros and enforces it.\r\n     *  @param _disputeID ID of Kleros dispute.\r\n     *  @param _ruling Ruling that is given by Kleros. This needs to be converted to Realitio answer before reporting the answer by shifting by 1.\r\n     */\r\n    function rule(uint256 _disputeID, uint256 _ruling) public override {\r\n        require(IArbitrator(msg.sender) == arbitrator, \"Only arbitrator allowed\");\r\n\r\n        uint256 questionID = externalIDtoLocalID[_disputeID];\r\n        ArbitrationRequest storage arbitrationRequest = arbitrationRequests[questionID];\r\n\r\n        require(arbitrationRequest.status == Status.Disputed, \"Invalid arbitration status\");\r\n\r\n        Round storage round = arbitrationRequest.rounds[arbitrationRequest.rounds.length - 1];\r\n\r\n        // If there is only one ruling option in last round that is fully funded, no matter what Kleros ruling was this ruling option is the winner by default.\r\n        uint256 finalRuling = (round.fundedRulings.length == 1) ? round.fundedRulings[0] : _ruling;\r\n\r\n        arbitrationRequest.ruling = finalRuling;\r\n        arbitrationRequest.status = Status.Ruled;\r\n\r\n        emit Ruling(IArbitrator(msg.sender), _disputeID, finalRuling);\r\n\r\n        // Ready to call `reportAnswer` now.\r\n    }\r\n\r\n    /** @dev TRUSTED. Manages crowdfunded appeals contributions and calls appeal function of the Kleros arbitrator to appeal a dispute.\r\n     *  @param _questionID Identifier of the Realitio question, casted to uint. This also serves as the local identifier in this contract.\r\n     *  @param _ruling The ruling option to which the caller wants to contribute to.\r\n     *  @return fullyFunded True if the ruling option got fully funded as a result of this contribution.\r\n     */\r\n    function fundAppeal(uint256 _questionID, uint256 _ruling) external payable override returns (bool fullyFunded) {\r\n        ArbitrationRequest storage arbitrationRequest = arbitrationRequests[_questionID];\r\n        require(arbitrationRequest.status == Status.Disputed, \"No dispute to appeal.\");\r\n\r\n        uint256 disputeID = arbitrationRequest.disputeID;\r\n        uint256 currentRuling = arbitrator.currentRuling(disputeID);\r\n        uint256 originalCost;\r\n        uint256 totalCost;\r\n        {\r\n            (uint256 originalStart, uint256 originalEnd) = arbitrator.appealPeriod(disputeID);\r\n\r\n            uint256 multiplier;\r\n\r\n            if (_ruling == currentRuling) {\r\n                require(block.timestamp < originalEnd, \"Funding must be made within the appeal period.\");\r\n\r\n                multiplier = WINNER_STAKE_MULTIPLIER;\r\n            } else {\r\n                require(block.timestamp < (originalStart + ((originalEnd - originalStart) / 2)), \"Funding must be made within the first half appeal period.\");\r\n\r\n                multiplier = LOSER_STAKE_MULTIPLIER;\r\n            }\r\n\r\n            originalCost = arbitrator.appealCost(disputeID, arbitratorExtraData);\r\n            totalCost = originalCost.addCap(originalCost.mulCap(multiplier) / MULTIPLIER_DENOMINATOR);\r\n        }\r\n\r\n        uint256 lastRoundIndex = arbitrationRequest.rounds.length - 1;\r\n        Round storage lastRound = arbitrationRequest.rounds[lastRoundIndex];\r\n        require(!lastRound.hasPaid[_ruling], \"Appeal fee has already been paid.\");\r\n\r\n        uint256 contribution = totalCost.subCap(lastRound.paidFees[_ruling]) > msg.value ? msg.value : totalCost.subCap(lastRound.paidFees[_ruling]);\r\n        emit Contribution(_questionID, lastRoundIndex, _ruling, msg.sender, contribution);\r\n\r\n        lastRound.contributions[msg.sender][_ruling] += contribution;\r\n        lastRound.paidFees[_ruling] += contribution;\r\n\r\n        if (lastRound.paidFees[_ruling] >= totalCost) {\r\n            lastRound.feeRewards += lastRound.paidFees[_ruling];\r\n            lastRound.fundedRulings.push(_ruling);\r\n            lastRound.hasPaid[_ruling] = true;\r\n            emit RulingFunded(_questionID, lastRoundIndex, _ruling);\r\n        }\r\n\r\n        if (lastRound.fundedRulings.length == 2) {\r\n            // At least two ruling options are fully funded.\r\n            arbitrationRequest.rounds.push();\r\n\r\n            lastRound.feeRewards = lastRound.feeRewards.subCap(originalCost);\r\n            arbitrator.appeal{value: originalCost}(disputeID, arbitratorExtraData);\r\n        }\r\n\r\n        if (msg.value.subCap(contribution) > 0) msg.sender.send(msg.value.subCap(contribution)); // Sending extra value back to contributor.\r\n\r\n        return lastRound.hasPaid[_ruling];\r\n    }\r\n\r\n    /** @dev Returns number of possible ruling options. Valid rulings are [0, count].\r\n     *  @return count The number of ruling options.\r\n     */\r\n    function numberOfRulingOptions(uint256) external pure override returns (uint256 count) {\r\n        return NUMBER_OF_RULING_OPTIONS;\r\n    }\r\n\r\n    /** @dev Returns arbitration fee by calling arbitrationCost function in the arbitrator contract.\r\n     *  @return fee Arbitration fee that needs to be paid.\r\n     */\r\n    function getDisputeFee(bytes32) external view override returns (uint256 fee) {\r\n        return arbitrator.arbitrationCost(arbitratorExtraData);\r\n    }\r\n\r\n    /** @dev Returns multipliers for appeals.\r\n     *  @return _WINNER_STAKE_MULTIPLIER Winners stake multiplier.\r\n     *  @return _LOSER_STAKE_MULTIPLIER Losers stake multiplier.\r\n     *  @return _LOSER_APPEAL_PERIOD_MULTIPLIER Losers appeal period multiplier. The loser is given less time to fund its appeal to defend against last minute appeal funding attacks.\r\n     *  @return _DENOMINATOR Multiplier denominator in basis points. Required for achieving floating-point-like behavior.\r\n     */\r\n    function getMultipliers()\r\n        external\r\n        pure\r\n        override\r\n        returns (\r\n            uint256 _WINNER_STAKE_MULTIPLIER,\r\n            uint256 _LOSER_STAKE_MULTIPLIER,\r\n            uint256 _LOSER_APPEAL_PERIOD_MULTIPLIER,\r\n            uint256 _DENOMINATOR\r\n        )\r\n    {\r\n        return (WINNER_STAKE_MULTIPLIER, LOSER_STAKE_MULTIPLIER, LOSER_APPEAL_PERIOD_MULTIPLIER, MULTIPLIER_DENOMINATOR);\r\n    }\r\n\r\n    /** @dev Allows to withdraw any rewards or reimbursable fees after the dispute gets resolved. For all rounds at once.\r\n     *  This function has O(m) time complexity where m is number of rounds.\r\n     *  It is safe to assume m is always less than 10 as appeal cost growth order is O(2^m).\r\n     *  @param _questionID Identifier of the Realitio question, casted to uint. This also serves as the local identifier in this contract.\r\n     *  @param _contributor The address whose rewards to withdraw.\r\n     *  @param _ruling Ruling that received contributions from contributor.\r\n     */\r\n    function withdrawFeesAndRewardsForAllRounds(\r\n        uint256 _questionID,\r\n        address payable _contributor,\r\n        uint256 _ruling\r\n    ) external override {\r\n        ArbitrationRequest storage arbitrationRequest = arbitrationRequests[_questionID];\r\n        uint256 noOfRounds = arbitrationRequest.rounds.length;\r\n\r\n        for (uint256 roundNumber = 0; roundNumber < noOfRounds; roundNumber++) {\r\n            withdrawFeesAndRewards(_questionID, _contributor, roundNumber, _ruling);\r\n        }\r\n    }\r\n\r\n    /** @dev Allows to withdraw any reimbursable fees or rewards after the dispute gets resolved.\r\n     *  @param _questionID Identifier of the Realitio question, casted to uint. This also serves as the local identifier in this contract.\r\n     *  @param _contributor The address whose rewards to withdraw.\r\n     *  @param _roundNumber The number of the round caller wants to withdraw from.\r\n     *  @param _ruling Ruling that received contribution from contributor.\r\n     *  @return amount The amount available to withdraw for given question, contributor, round number and ruling option.\r\n     */\r\n    function withdrawFeesAndRewards(\r\n        uint256 _questionID,\r\n        address payable _contributor,\r\n        uint256 _roundNumber,\r\n        uint256 _ruling\r\n    ) public override returns (uint256 amount) {\r\n        ArbitrationRequest storage arbitrationRequest = arbitrationRequests[_questionID];\r\n        require(arbitrationRequest.status > Status.Disputed, \"There is no ruling yet.\");\r\n\r\n        Round storage round = arbitrationRequest.rounds[_roundNumber];\r\n\r\n        amount = getWithdrawableAmount(round, _contributor, _ruling, arbitrationRequest.ruling);\r\n\r\n        if (amount != 0) {\r\n            round.contributions[_contributor][_ruling] = 0;\r\n            _contributor.send(amount); // Ignoring failure condition deliberately.\r\n            emit Withdrawal(_questionID, _roundNumber, _ruling, _contributor, amount);\r\n        }\r\n    }\r\n\r\n    /** @dev Returns the sum of withdrawable amount.\r\n     *  This function has O(m) time complexity where m is number of rounds.\r\n     *  It is safe to assume m is always less than 10 as appeal cost growth order is O(m^2).\r\n     *  @param _questionID Identifier of the Realitio question, casted to uint. This also serves as the local identifier in this contract.\r\n     *  @param _contributor The contributor for which to query.\r\n     *  @param _ruling Ruling option to look for potential withdrawals.\r\n     *  @return sum The total amount available to withdraw.\r\n     */\r\n    function getTotalWithdrawableAmount(\r\n        uint256 _questionID,\r\n        address payable _contributor,\r\n        uint256 _ruling\r\n    ) external view override returns (uint256 sum) {\r\n        ArbitrationRequest storage arbitrationRequest = arbitrationRequests[_questionID];\r\n        if (arbitrationRequest.status < Status.Ruled) return 0;\r\n        uint256 noOfRounds = arbitrationRequest.rounds.length;\r\n        uint256 finalRuling = arbitrationRequest.ruling;\r\n\r\n        for (uint256 roundNumber = 0; roundNumber < noOfRounds; roundNumber++) {\r\n            Round storage round = arbitrationRequest.rounds[roundNumber];\r\n            sum += getWithdrawableAmount(round, _contributor, _ruling, finalRuling);\r\n        }\r\n    }\r\n\r\n    /** @dev Returns withdrawable amount for given parameters.\r\n     *  @param _round The round to calculate amount for.\r\n     *  @param _contributor The contributor for which to query.\r\n     *  @param _ruling The ruling option to search for potential withdrawal.\r\n     *  @param _finalRuling Final ruling given by arbitrator.\r\n     *  @return amount Amount available to withdraw for given ruling option.\r\n     */\r\n    function getWithdrawableAmount(\r\n        Round storage _round,\r\n        address _contributor,\r\n        uint256 _ruling,\r\n        uint256 _finalRuling\r\n    ) internal view returns (uint256 amount) {\r\n        if (!_round.hasPaid[_ruling]) {\r\n            // Allow to reimburse if funding was unsuccessful for this ruling option.\r\n            amount = _round.contributions[_contributor][_ruling];\r\n        } else {\r\n            // Funding was successful for this ruling option.\r\n            if (_ruling == _finalRuling) {\r\n                // This ruling option is the ultimate winner.\r\n                amount = _round.paidFees[_ruling] > 0 ? (_round.contributions[_contributor][_ruling] * _round.feeRewards) / _round.paidFees[_ruling] : 0;\r\n            } else if (!_round.hasPaid[_finalRuling]) {\r\n                // The ultimate winner was not funded in this round. Contributions discounting the appeal fee are reimbursed proportionally.\r\n                amount = (_round.contributions[_contributor][_ruling] * _round.feeRewards) / (_round.paidFees[_round.fundedRulings[0]] + _round.paidFees[_round.fundedRulings[1]]);\r\n            }\r\n        }\r\n    }\r\n}\r\n"
    },
    "contracts/IRealitioArbitrator.sol": {
      "content": "// SPDX-License-Identifier: MIT\r\n\r\n/**\r\n *  @authors: [@ferittuncer]\r\n *  @reviewers: [@hbarcelos]\r\n *  @auditors: []\r\n *  @bounties: []\r\n *  @deployments: []\r\n */\r\n\r\npragma solidity ^0.7.6;\r\nimport \"./IRealitio.sol\";\r\n\r\n/**\r\n *  @title IRealitioArbitrator\r\n *  @dev Based on https://github.com/realitio/realitio-dapp/blob/1860548a51f52eba4930baad051f811e9f7adaee/docs/arbitrators.rst\r\n */\r\ninterface IRealitioArbitrator {\r\n    /** @dev Returns Realitio implementation instance.\r\n     */\r\n    function realitio() external view returns (IRealitio);\r\n\r\n    /** @dev Provides a string of json-encoded metadata. The following properties are scheduled for implementation in the Reality.eth dapp:\r\n        tos: A URI representing the location of a terms-of-service document for the arbitrator.\r\n        template_hashes: An array of hashes of templates supported by the arbitrator. If you have a numerical ID for a template registered with Reality.eth, you can look up this hash by calling the Reality.eth template_hashes() function.\r\n     */\r\n    function metadata() external view returns (string calldata);\r\n\r\n    /** @dev Returns arbitrators fee for arbitrating this question.\r\n     */\r\n    function getDisputeFee(bytes32 questionID) external view returns (uint256);\r\n}\r\n"
    },
    "@kleros/dispute-resolver-interface-contract/contracts/solc-0.7.x/IDisputeResolver.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\n/**\n *  @authors: [@ferittuncer]\n *  @reviewers: [@mtsalenc*, @hbarcelos*, @unknownunknown1*, @MerlinEgalite*]\n *  @auditors: []\n *  @bounties: []\n *  @deployments: []\n */\n\npragma solidity ^0.7.0;\n\nimport \"@kleros/erc-792/contracts/IArbitrable.sol\";\nimport \"@kleros/erc-792/contracts/erc-1497/IEvidence.sol\";\nimport \"@kleros/erc-792/contracts/IArbitrator.sol\";\n\n/**\n *  @title This serves as a standard interface for crowdfunded appeals and evidence submission, which aren't a part of the arbitration (erc-792 and erc-1497) standard yet.\n    This interface is used in Dispute Resolver (resolve.kleros.io).\n */\nabstract contract IDisputeResolver is IArbitrable, IEvidence {\n    string public constant VERSION = \"2.0.0\"; // Can be used to distinguish between multiple deployed versions, if necessary.\n\n    /** @dev Raised when a contribution is made, inside fundAppeal function.\n     *  @param _localDisputeID Identifier of a dispute in scope of arbitrable contract. Arbitrator ids can be translated to local ids via externalIDtoLocalID.\n     *  @param _round The round number the contribution was made to.\n     *  @param ruling Indicates the ruling option which got the contribution.\n     *  @param _contributor Caller of fundAppeal function.\n     *  @param _amount Contribution amount.\n     */\n    event Contribution(uint256 indexed _localDisputeID, uint256 indexed _round, uint256 ruling, address indexed _contributor, uint256 _amount);\n\n    /** @dev Raised when a contributor withdraws non-zero value.\n     *  @param _localDisputeID Identifier of a dispute in scope of arbitrable contract. Arbitrator ids can be translated to local ids via externalIDtoLocalID.\n     *  @param _round The round number the withdrawal was made from.\n     *  @param _ruling Indicates the ruling option which contributor gets rewards from.\n     *  @param _contributor The beneficiary of withdrawal.\n     *  @param _reward Total amount of withdrawal, consists of reimbursed deposits plus rewards.\n     */\n    event Withdrawal(uint256 indexed _localDisputeID, uint256 indexed _round, uint256 _ruling, address indexed _contributor, uint256 _reward);\n\n    /** @dev To be raised when a ruling option is fully funded for appeal.\n     *  @param _localDisputeID Identifier of a dispute in scope of arbitrable contract. Arbitrator ids can be translated to local ids via externalIDtoLocalID.\n     *  @param _round Number of the round this ruling option was fully funded in.\n     *  @param _ruling The ruling option which just got fully funded.\n     */\n    event RulingFunded(uint256 indexed _localDisputeID, uint256 indexed _round, uint256 indexed _ruling);\n\n    /** @dev Maps external (arbitrator side) dispute id to local (arbitrable) dispute id.\n     *  @param _externalDisputeID Dispute id as in arbitrator contract.\n     *  @return localDisputeID Dispute id as in arbitrable contract.\n     */\n    function externalIDtoLocalID(uint256 _externalDisputeID) external virtual returns (uint256 localDisputeID);\n\n    /** @dev Returns number of possible ruling options. Valid rulings are [0, return value].\n     *  @param _localDisputeID Identifier of a dispute in scope of arbitrable contract. Arbitrator ids can be translated to local ids via externalIDtoLocalID.\n     *  @return count The number of ruling options.\n     */\n    function numberOfRulingOptions(uint256 _localDisputeID) external view virtual returns (uint256 count);\n\n    /** @dev Allows to submit evidence for a given dispute.\n     *  @param _localDisputeID Identifier of a dispute in scope of arbitrable contract. Arbitrator ids can be translated to local ids via externalIDtoLocalID.\n     *  @param _evidenceURI IPFS path to evidence, example: '/ipfs/Qmarwkf7C9RuzDEJNnarT3WZ7kem5bk8DZAzx78acJjMFH/evidence.json'\n     */\n    function submitEvidence(uint256 _localDisputeID, string calldata _evidenceURI) external virtual;\n\n    /** @dev Manages contributions and calls appeal function of the specified arbitrator to appeal a dispute. This function lets appeals be crowdfunded.\n     *  @param _localDisputeID Identifier of a dispute in scope of arbitrable contract. Arbitrator ids can be translated to local ids via externalIDtoLocalID.\n     *  @param _ruling The ruling option to which the caller wants to contribute.\n     *  @return fullyFunded True if the ruling option got fully funded as a result of this contribution.\n     */\n    function fundAppeal(uint256 _localDisputeID, uint256 _ruling) external payable virtual returns (bool fullyFunded);\n\n    /** @dev Returns appeal multipliers.\n     *  @return winnerStakeMultiplier Winners stake multiplier.\n     *  @return loserStakeMultiplier Losers stake multiplier.\n     *  @return loserAppealPeriodMultiplier Losers appeal period multiplier. The loser is given less time to fund its appeal to defend against last minute appeal funding attacks.\n     *  @return denominator Multiplier denominator in basis points.\n     */\n    function getMultipliers()\n        external\n        view\n        virtual\n        returns (\n            uint256 winnerStakeMultiplier,\n            uint256 loserStakeMultiplier,\n            uint256 loserAppealPeriodMultiplier,\n            uint256 denominator\n        );\n\n    /** @dev Allows to withdraw any reimbursable fees or rewards after the dispute gets resolved.\n     *  @param _localDisputeID Identifier of a dispute in scope of arbitrable contract. Arbitrator ids can be translated to local ids via externalIDtoLocalID.\n     *  @param _contributor Beneficiary of withdraw operation.\n     *  @param _round Number of the round that caller wants to execute withdraw on.\n     *  @param _ruling A ruling option that caller wants to execute withdraw on.\n     *  @return sum The amount that is going to be transferred to contributor as a result of this function call.\n     */\n    function withdrawFeesAndRewards(\n        uint256 _localDisputeID,\n        address payable _contributor,\n        uint256 _round,\n        uint256 _ruling\n    ) external virtual returns (uint256 sum);\n\n    /** @dev Allows to withdraw any rewards or reimbursable fees after the dispute gets resolved. For multiple rulings options and for all rounds at once.\n     *  @param _localDisputeID Identifier of a dispute in scope of arbitrable contract. Arbitrator ids can be translated to local ids via externalIDtoLocalID.\n     *  @param _contributor Beneficiary of withdraw operation.\n     *  @param _ruling Ruling option that caller wants to execute withdraw on.\n     */\n    function withdrawFeesAndRewardsForAllRounds(\n        uint256 _localDisputeID,\n        address payable _contributor,\n        uint256 _ruling\n    ) external virtual;\n\n    /** @dev Returns the sum of withdrawable amount.\n     *  @param _localDisputeID Identifier of a dispute in scope of arbitrable contract. Arbitrator ids can be translated to local ids via externalIDtoLocalID.\n     *  @param _contributor Beneficiary of withdraw operation.\n     *  @param _ruling Ruling option that caller wants to get withdrawable amount from.\n     *  @return sum The total amount available to withdraw.\n     */\n    function getTotalWithdrawableAmount(\n        uint256 _localDisputeID,\n        address payable _contributor,\n        uint256 _ruling\n    ) external view virtual returns (uint256 sum);\n}\n"
    },
    "@kleros/ethereum-libraries/contracts/CappedMath.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\n/**\n * @authors: [@mtsalenc, @hbarcelos]\n * @reviewers: [@clesaege*, @ferittuncer]\n * @auditors: []\n * @bounties: []\n * @deployments: []\n * SPDX-License-Identifier: MIT\n */\n\npragma solidity ^0.7.6;\n\n\n/**\n * @title CappedMath\n * @dev Math operations with caps for under and overflow.\n */\nlibrary CappedMath {\n    uint constant private UINT_MAX = 2**256 - 1;\n\n    /**\n     * @dev Adds two unsigned integers, returns 2^256 - 1 on overflow.\n     */\n    function addCap(uint _a, uint _b) internal pure returns (uint) {\n        uint c = _a + _b;\n        return c >= _a ? c : UINT_MAX;\n    }\n\n    /**\n     * @dev Subtracts two integers, returns 0 on underflow.\n     */\n    function subCap(uint _a, uint _b) internal pure returns (uint) {\n        if (_b > _a)\n            return 0;\n        else\n            return _a - _b;\n    }\n\n    /**\n     * @dev Multiplies two unsigned integers, returns 2^256 - 1 on overflow.\n     */\n    function mulCap(uint _a, uint _b) internal pure returns (uint) {\n        // Gas optimization: this is cheaper than requiring '_a' not being zero, but the\n        // benefit is lost if '_b' is also tested.\n        // See: https://github.com/OpenZeppelin/openzeppelin-solidity/pull/522\n        if (_a == 0)\n            return 0;\n\n        uint c = _a * _b;\n        return c / _a == _b ? c : UINT_MAX;\n    }\n}\n"
    },
    "@kleros/erc-792/contracts/IArbitrable.sol": {
      "content": "/**\n * @authors: [@ferittuncer, @hbarcelos]\n * @reviewers: [@remedcu*]\n * @auditors: []\n * @bounties: []\n * @deployments: []\n * SPDX-License-Identifier: MIT\n */\npragma solidity >=0.7;\n\nimport \"./IArbitrator.sol\";\n\n/**\n * @title IArbitrable\n * Arbitrable interface.\n * When developing arbitrable contracts, we need to:\n * - Define the action taken when a ruling is received by the contract.\n * - Allow dispute creation. For this a function must call arbitrator.createDispute{value: _fee}(_choices,_extraData);\n */\ninterface IArbitrable {\n    /**\n     * @dev To be raised when a ruling is given.\n     * @param _arbitrator The arbitrator giving the ruling.\n     * @param _disputeID ID of the dispute in the Arbitrator contract.\n     * @param _ruling The ruling which was given.\n     */\n    event Ruling(IArbitrator indexed _arbitrator, uint256 indexed _disputeID, uint256 _ruling);\n\n    /**\n     * @dev Give a ruling for a dispute. Must be called by the arbitrator.\n     * The purpose of this function is to ensure that the address calling it has the right to rule on the contract.\n     * @param _disputeID ID of the dispute in the Arbitrator contract.\n     * @param _ruling Ruling given by the arbitrator. Note that 0 is reserved for \"Not able/wanting to make a decision\".\n     */\n    function rule(uint256 _disputeID, uint256 _ruling) external;\n}\n"
    },
    "@kleros/erc-792/contracts/erc-1497/IEvidence.sol": {
      "content": "/**\n * @authors: [@ferittuncer, @hbarcelos]\n * @reviewers: []\n * @auditors: []\n * @bounties: []\n * @deployments: []\n * SPDX-License-Identifier: MIT\n */\npragma solidity >=0.7;\n\nimport \"../IArbitrator.sol\";\n\n/** @title IEvidence\n *  ERC-1497: Evidence Standard\n */\ninterface IEvidence {\n    /**\n     * @dev To be emitted when meta-evidence is submitted.\n     * @param _metaEvidenceID Unique identifier of meta-evidence.\n     * @param _evidence A link to the meta-evidence JSON.\n     */\n    event MetaEvidence(uint256 indexed _metaEvidenceID, string _evidence);\n\n    /**\n     * @dev To be raised when evidence is submitted. Should point to the resource (evidences are not to be stored on chain due to gas considerations).\n     * @param _arbitrator The arbitrator of the contract.\n     * @param _evidenceGroupID Unique identifier of the evidence group the evidence belongs to.\n     * @param _party The address of the party submiting the evidence. Note that 0x0 refers to evidence not submitted by any party.\n     * @param _evidence A URI to the evidence JSON file whose name should be its keccak256 hash followed by .json.\n     */\n    event Evidence(\n        IArbitrator indexed _arbitrator,\n        uint256 indexed _evidenceGroupID,\n        address indexed _party,\n        string _evidence\n    );\n\n    /**\n     * @dev To be emitted when a dispute is created to link the correct meta-evidence to the disputeID.\n     * @param _arbitrator The arbitrator of the contract.\n     * @param _disputeID ID of the dispute in the Arbitrator contract.\n     * @param _metaEvidenceID Unique identifier of meta-evidence.\n     * @param _evidenceGroupID Unique identifier of the evidence group that is linked to this dispute.\n     */\n    event Dispute(\n        IArbitrator indexed _arbitrator,\n        uint256 indexed _disputeID,\n        uint256 _metaEvidenceID,\n        uint256 _evidenceGroupID\n    );\n}\n"
    },
    "@kleros/erc-792/contracts/IArbitrator.sol": {
      "content": "/**\n * @authors: [@ferittuncer, @hbarcelos]\n * @reviewers: [@remedcu*]\n * @auditors: []\n * @bounties: []\n * @deployments: []\n * SPDX-License-Identifier: MIT\n */\n\npragma solidity >=0.7;\n\nimport \"./IArbitrable.sol\";\n\n/**\n * @title Arbitrator\n * Arbitrator abstract contract.\n * When developing arbitrator contracts we need to:\n * - Define the functions for dispute creation (createDispute) and appeal (appeal). Don't forget to store the arbitrated contract and the disputeID (which should be unique, may nbDisputes).\n * - Define the functions for cost display (arbitrationCost and appealCost).\n * - Allow giving rulings. For this a function must call arbitrable.rule(disputeID, ruling).\n */\ninterface IArbitrator {\n    enum DisputeStatus {Waiting, Appealable, Solved}\n\n    /**\n     * @dev To be emitted when a dispute is created.\n     * @param _disputeID ID of the dispute.\n     * @param _arbitrable The contract which created the dispute.\n     */\n    event DisputeCreation(uint256 indexed _disputeID, IArbitrable indexed _arbitrable);\n\n    /**\n     * @dev To be emitted when a dispute can be appealed.\n     * @param _disputeID ID of the dispute.\n     * @param _arbitrable The contract which created the dispute.\n     */\n    event AppealPossible(uint256 indexed _disputeID, IArbitrable indexed _arbitrable);\n\n    /**\n     * @dev To be emitted when the current ruling is appealed.\n     * @param _disputeID ID of the dispute.\n     * @param _arbitrable The contract which created the dispute.\n     */\n    event AppealDecision(uint256 indexed _disputeID, IArbitrable indexed _arbitrable);\n\n    /**\n     * @dev Create a dispute. Must be called by the arbitrable contract.\n     * Must be paid at least arbitrationCost(_extraData).\n     * @param _choices Amount of choices the arbitrator can make in this dispute.\n     * @param _extraData Can be used to give additional info on the dispute to be created.\n     * @return disputeID ID of the dispute created.\n     */\n    function createDispute(uint256 _choices, bytes calldata _extraData) external payable returns (uint256 disputeID);\n\n    /**\n     * @dev Compute the cost of arbitration. It is recommended not to increase it often, as it can be highly time and gas consuming for the arbitrated contracts to cope with fee augmentation.\n     * @param _extraData Can be used to give additional info on the dispute to be created.\n     * @return cost Amount to be paid.\n     */\n    function arbitrationCost(bytes calldata _extraData) external view returns (uint256 cost);\n\n    /**\n     * @dev Appeal a ruling. Note that it has to be called before the arbitrator contract calls rule.\n     * @param _disputeID ID of the dispute to be appealed.\n     * @param _extraData Can be used to give extra info on the appeal.\n     */\n    function appeal(uint256 _disputeID, bytes calldata _extraData) external payable;\n\n    /**\n     * @dev Compute the cost of appeal. It is recommended not to increase it often, as it can be higly time and gas consuming for the arbitrated contracts to cope with fee augmentation.\n     * @param _disputeID ID of the dispute to be appealed.\n     * @param _extraData Can be used to give additional info on the dispute to be created.\n     * @return cost Amount to be paid.\n     */\n    function appealCost(uint256 _disputeID, bytes calldata _extraData) external view returns (uint256 cost);\n\n    /**\n     * @dev Compute the start and end of the dispute's current or next appeal period, if possible. If not known or appeal is impossible: should return (0, 0).\n     * @param _disputeID ID of the dispute.\n     * @return start The start of the period.\n     * @return end The end of the period.\n     */\n    function appealPeriod(uint256 _disputeID) external view returns (uint256 start, uint256 end);\n\n    /**\n     * @dev Return the status of a dispute.\n     * @param _disputeID ID of the dispute to rule.\n     * @return status The status of the dispute.\n     */\n    function disputeStatus(uint256 _disputeID) external view returns (DisputeStatus status);\n\n    /**\n     * @dev Return the current ruling of a dispute. This is useful for parties to know if they should appeal.\n     * @param _disputeID ID of the dispute.\n     * @return ruling The ruling which has been given or the one which will be given if there is no appeal.\n     */\n    function currentRuling(uint256 _disputeID) external view returns (uint256 ruling);\n}\n"
    },
    "contracts/test/AutoAppealableArbitratorl.sol": {
      "content": "// SPDX-License-Identifier: MIT\r\n\r\n/**\r\n *  @authors: [@clesaege]\r\n *  @reviewers: []\r\n *  @auditors: []\r\n *  @bounties: []\r\n *  @deployments: []\r\n */\r\n\r\npragma solidity >=0.7;\r\n\r\nimport \"@kleros/erc-792/contracts/IArbitrable.sol\";\r\nimport \"@kleros/erc-792/contracts/IArbitrator.sol\";\r\nimport \"@kleros/ethereum-libraries/contracts/CappedMath.sol\";\r\n\r\n/** @title Auto Appealable Arbitrator\r\n *  @dev This is a centralized arbitrator which either gives direct rulings or provides a time and fee for appeal.\r\n */\r\ncontract AutoAppealableArbitrator is IArbitrator {\r\n    using CappedMath for uint256; // Operations bounded between 0 and 2**256 - 1.\r\n\r\n    address public owner = msg.sender;\r\n    uint256 arbitrationPrice; // Not public because arbitrationCost already acts as an accessor.\r\n    uint256 constant NOT_PAYABLE_VALUE = (2**256 - 2) / 2; // High value to be sure that the appeal is too expensive.\r\n\r\n    struct Dispute {\r\n        IArbitrable arbitrated; // The contract requiring arbitration.\r\n        uint256 choices; // The amount of possible choices, 0 excluded.\r\n        uint256 fees; // The total amount of fees collected by the arbitrator.\r\n        uint256 ruling; // The current ruling.\r\n        DisputeStatus status; // The status of the dispute.\r\n        uint256 appealCost; // The cost to appeal. 0 before it is appealable.\r\n        uint256 appealPeriodStart; // The start of the appeal period. 0 before it is appealable.\r\n        uint256 appealPeriodEnd; // The end of the appeal Period. 0 before it is appealable.\r\n    }\r\n\r\n    modifier onlyOwner {\r\n        require(msg.sender == owner, \"Can only be called by the owner.\");\r\n        _;\r\n    }\r\n\r\n    Dispute[] public disputes;\r\n\r\n    /** @dev Constructor. Set the initial arbitration price.\r\n     *  @param _arbitrationPrice Amount to be paid for arbitration.\r\n     */\r\n    constructor(uint256 _arbitrationPrice) public {\r\n        arbitrationPrice = _arbitrationPrice;\r\n    }\r\n\r\n    /** @dev Set the arbitration price. Only callable by the owner.\r\n     *  @param _arbitrationPrice Amount to be paid for arbitration.\r\n     */\r\n    function setArbitrationPrice(uint256 _arbitrationPrice) external onlyOwner {\r\n        arbitrationPrice = _arbitrationPrice;\r\n    }\r\n\r\n    /** @dev Cost of arbitration. Accessor to arbitrationPrice.\r\n     *  @return fee Amount to be paid.\r\n     */\r\n    function arbitrationCost(bytes memory) public view override returns (uint256 fee) {\r\n        return arbitrationPrice;\r\n    }\r\n\r\n    /** @dev Cost of appeal. If appeal is not possible, it's a high value which can never be paid.\r\n     *  @param _disputeID ID of the dispute to be appealed.\r\n     *  @return fee Amount to be paid.\r\n     */\r\n    function appealCost(uint256 _disputeID, bytes memory) public view override returns (uint256 fee) {\r\n        Dispute storage dispute = disputes[_disputeID];\r\n        if (dispute.status == DisputeStatus.Appealable) return dispute.appealCost;\r\n        else return NOT_PAYABLE_VALUE;\r\n    }\r\n\r\n    /** @dev Create a dispute. Must be called by the arbitrable contract.\r\n     *  Must be paid at least arbitrationCost().\r\n     *  @param _choices Amount of choices the arbitrator can make in this dispute. When ruling <= choices.\r\n     *  @param _extraData Can be used to give additional info on the dispute to be created.\r\n     *  @return disputeID ID of the dispute created.\r\n     */\r\n    function createDispute(uint256 _choices, bytes memory _extraData) public payable override returns (uint256 disputeID) {\r\n        uint256 arbitrationFee = arbitrationCost(_extraData);\r\n        require(msg.value >= arbitrationFee, \"Value is less than required arbitration fee.\");\r\n        disputes.push(\r\n            Dispute({arbitrated: IArbitrable(msg.sender), choices: _choices, fees: msg.value, ruling: 0, status: DisputeStatus.Waiting, appealCost: 0, appealPeriodStart: 0, appealPeriodEnd: 0})\r\n        ); // Create the dispute and return its number.\r\n        disputeID = disputes.length - 1;\r\n        emit DisputeCreation(disputeID, IArbitrable(msg.sender));\r\n    }\r\n\r\n    /** @dev Give a ruling. UNTRUSTED.\r\n     *  @param _disputeID ID of the dispute to rule.\r\n     *  @param _ruling Ruling given by the arbitrator. Note that 0 means \"Not able/wanting to make a decision\".\r\n     */\r\n    function giveRuling(uint256 _disputeID, uint256 _ruling) external onlyOwner {\r\n        Dispute storage dispute = disputes[_disputeID];\r\n        require(_ruling <= dispute.choices, \"Invalid ruling.\");\r\n        require(dispute.status == DisputeStatus.Waiting, \"The dispute must be waiting for arbitration.\");\r\n\r\n        dispute.ruling = _ruling;\r\n        dispute.status = DisputeStatus.Solved;\r\n\r\n        msg.sender.send(dispute.fees); // Avoid blocking.\r\n        dispute.arbitrated.rule(_disputeID, _ruling);\r\n    }\r\n\r\n    /** @dev Give an appealable ruling.\r\n     *  @param _disputeID ID of the dispute to rule.\r\n     *  @param _ruling Ruling given by the arbitrator. Note that 0 means \"Not able/wanting to make a decision\".\r\n     *  @param _appealCost The cost of appeal.\r\n     *  @param _timeToAppeal The time to appeal the ruling.\r\n     */\r\n    function giveAppealableRuling(\r\n        uint256 _disputeID,\r\n        uint256 _ruling,\r\n        uint256 _appealCost,\r\n        uint256 _timeToAppeal\r\n    ) external onlyOwner {\r\n        Dispute storage dispute = disputes[_disputeID];\r\n        require(_ruling <= dispute.choices, \"Invalid ruling.\");\r\n        require(dispute.status == DisputeStatus.Waiting, \"The dispute must be waiting for arbitration.\");\r\n\r\n        dispute.ruling = _ruling;\r\n        dispute.status = DisputeStatus.Appealable;\r\n        dispute.appealCost = _appealCost;\r\n        dispute.appealPeriodStart = block.timestamp;\r\n        dispute.appealPeriodEnd = block.timestamp.addCap(_timeToAppeal);\r\n\r\n        emit AppealPossible(_disputeID, dispute.arbitrated);\r\n    }\r\n\r\n    /** @dev Change the appeal fee of a dispute.\r\n     *  @param _disputeID The ID of the dispute to update.\r\n     *  @param _appealCost The new cost to appeal this ruling.\r\n     */\r\n    function changeAppealFee(uint256 _disputeID, uint256 _appealCost) external onlyOwner {\r\n        Dispute storage dispute = disputes[_disputeID];\r\n        require(dispute.status == DisputeStatus.Appealable, \"The dispute must be appealable.\");\r\n\r\n        dispute.appealCost = _appealCost;\r\n    }\r\n\r\n    /** @dev Appeal a ruling. Note that it has to be called before the arbitrator contract calls rule.\r\n     *  @param _disputeID ID of the dispute to be appealed.\r\n     *  @param _extraData Can be used to give extra info on the appeal.\r\n     */\r\n    function appeal(uint256 _disputeID, bytes memory _extraData) public payable override {\r\n        Dispute storage dispute = disputes[_disputeID];\r\n        uint256 appealFee = appealCost(_disputeID, _extraData);\r\n        require(dispute.status == DisputeStatus.Appealable, \"The dispute must be appealable.\");\r\n        require(block.timestamp < dispute.appealPeriodEnd, \"The appeal must occur before the end of the appeal period.\");\r\n        require(msg.value >= appealFee, \"Value is less than required appeal fee\");\r\n\r\n        dispute.appealPeriodStart = 0;\r\n        dispute.appealPeriodEnd = 0;\r\n        dispute.fees += msg.value;\r\n        dispute.status = DisputeStatus.Waiting;\r\n        emit AppealDecision(_disputeID, IArbitrable(msg.sender));\r\n    }\r\n\r\n    /** @dev Execute the ruling of a dispute after the appeal period has passed. UNTRUSTED.\r\n     *  @param _disputeID ID of the dispute to execute.\r\n     */\r\n    function executeRuling(uint256 _disputeID) external {\r\n        Dispute storage dispute = disputes[_disputeID];\r\n        require(dispute.status == DisputeStatus.Appealable, \"The dispute must be appealable.\");\r\n        require(block.timestamp >= dispute.appealPeriodEnd, \"The dispute must be executed after its appeal period has ended.\");\r\n\r\n        dispute.status = DisputeStatus.Solved;\r\n        msg.sender.send(dispute.fees); // Avoid blocking.\r\n        dispute.arbitrated.rule(_disputeID, dispute.ruling);\r\n    }\r\n\r\n    /** @dev Return the status of a dispute (in the sense of ERC792, not the Dispute property).\r\n     *  @param _disputeID ID of the dispute to rule.\r\n     *  @return status The status of the dispute.\r\n     */\r\n    function disputeStatus(uint256 _disputeID) public view override returns (DisputeStatus status) {\r\n        Dispute storage dispute = disputes[_disputeID];\r\n        if (disputes[_disputeID].status == DisputeStatus.Appealable && block.timestamp >= dispute.appealPeriodEnd)\r\n            // If the appeal period is over, consider it solved even if rule has not been called yet.\r\n            return DisputeStatus.Solved;\r\n        else return disputes[_disputeID].status;\r\n    }\r\n\r\n    /** @dev Return the ruling of a dispute.\r\n     *  @param _disputeID ID of the dispute.\r\n     *  @return ruling The ruling which have been given or which would be given if no appeals are raised.\r\n     */\r\n    function currentRuling(uint256 _disputeID) public view override returns (uint256 ruling) {\r\n        return disputes[_disputeID].ruling;\r\n    }\r\n\r\n    /** @dev Compute the start and end of the dispute's current or next appeal period, if possible.\r\n     *  @param _disputeID ID of the dispute.\r\n     *  @return start The start of the period.\r\n     *  @return end The End of the period.\r\n     */\r\n    function appealPeriod(uint256 _disputeID) public view override returns (uint256 start, uint256 end) {\r\n        Dispute storage dispute = disputes[_disputeID];\r\n        return (dispute.appealPeriodStart, dispute.appealPeriodEnd);\r\n    }\r\n}"
    },
    "contracts/Realitio_v2_0_ArbitratorWithAppeals.sol": {
      "content": "// SPDX-License-Identifier: MIT\r\n\r\n/**\r\n *  @authors: [@ferittuncer]\r\n *  @reviewers: [@unknownunknown1*, @hbarcelos, @MerlinEgalite, @shalzz, @fnanni-0, @clesaege, @jaybuidl]\r\n *  @auditors: []\r\n *  @bounties: []\r\n *  @deployments: []\r\n */\r\n\r\npragma solidity ^0.7.0;\r\n\r\nimport \"./IRealitio.sol\";\r\nimport \"./RealitioArbitratorWithAppealsBase.sol\";\r\n\r\n/**\r\n *  @title Realitio_v2_0_ArbitratorWithAppeals\r\n *  @dev A Realitio arbitrator implementation that uses Realitio v2.0 and Kleros.\r\n *  It notifies Realitio contract for arbitration requests and creates corresponding dispute on Kleros. Transmits Kleros ruling to Realitio contract.\r\n *  Maintains crowdfunded appeals and notifies Kleros contract. Provides a function to submit evidence for Kleros dispute.\r\n *  There is a conversion between Kleros ruling and Realitio answer and there is a need for shifting by 1. This is because ruling 0 in Kleros signals tie or no-ruling but in Realitio 0 is a valid answer.\r\n *  For reviewers this should be a focus as it's quite easy to get confused. Any mistakes on this conversion will render this contract useless.\r\n *  NOTE: This contract trusts the Kleros arbitrator and Realitio.\r\n */\r\ncontract Realitio_v2_0_ArbitratorWithAppeals is RealitioArbitratorWithAppealsBase {\r\n    /** @dev Constructor.\r\n     *  @param _realitio The address of the Realitio contract.\r\n     *  @param _metadata The metadata required for RealitioArbitrator.\r\n     *  @param _arbitrator The address of the ERC792 arbitrator.\r\n     *  @param _arbitratorExtraData The extra data used to raise a dispute in the ERC792 arbitrator.\r\n     *  @param _metaevidence Metaevidence as defined in ERC-1497.\r\n     */\r\n    constructor(\r\n        IRealitio _realitio,\r\n        string memory _metadata,\r\n        IArbitrator _arbitrator,\r\n        bytes memory _arbitratorExtraData,\r\n        string memory _metaevidence\r\n    ) RealitioArbitratorWithAppealsBase(_realitio, _metadata, _arbitrator, _arbitratorExtraData, _metaevidence) {}\r\n\r\n    /** @dev Compute winner and report the answer to a specified question from the ERC792 arbitrator to the Realitio v2.0 contract. TRUSTED.\r\n     *  @param _questionID The ID of the question.\r\n     *  @param _lastHistoryHash The history hash given with the last answer to the question in the Realitio contract.\r\n     *  @param _lastAnswerOrCommitmentID The last answer given, or its commitment ID if it was a commitment, to the question in the Realitio contract.\r\n     *  @param _lastBond The bond paid for the last answer to the question in the Realitio contract.\r\n     *  @param _lastAnswerer The last answerer to the question in the Realitio contract.\r\n     *  @param _isCommitment Whether the last answer to the question in the Realitio contract used commit or reveal or not. True if it did, false otherwise.\r\n     */\r\n    function computeWinnerAndReportAnswer(\r\n        bytes32 _questionID,\r\n        bytes32 _lastHistoryHash,\r\n        bytes32 _lastAnswerOrCommitmentID,\r\n        uint256 _lastBond,\r\n        address _lastAnswerer,\r\n        bool _isCommitment\r\n    ) external {\r\n        ArbitrationRequest storage arbitrationRequest = arbitrationRequests[uint256(_questionID)];\r\n        require(arbitrationRequest.status == Status.Ruled, \"The status should be Ruled.\");\r\n        require(\r\n            realitio.getHistoryHash(_questionID) == keccak256(abi.encodePacked(_lastHistoryHash, _lastAnswerOrCommitmentID, _lastBond, _lastAnswerer, _isCommitment)),\r\n            \"The hash of the history parameters supplied does not match the one stored in the Realitio contract.\"\r\n        ); // This is normally Realitio's responsibility to check but it does not, so we do instead. This is fixed in v2.1.\r\n\r\n        arbitrationRequest.status = Status.Reported;\r\n\r\n        // Note that ruling is shifted by -1 before calling Realitio. This works because 0-1 is equivalent to type(uint256).max. However, this won't be the case starting from Solidity 0.8.x.\r\n        // https://docs.soliditylang.org/en/v0.8.0/080-breaking-changes.html\r\n        realitio.submitAnswerByArbitrator(_questionID, bytes32(arbitrationRequest.ruling - 1), computeWinner(arbitrationRequest, _lastAnswerOrCommitmentID, _lastBond, _lastAnswerer, _isCommitment));\r\n    }\r\n\r\n    /** @dev Computes the Realitio answerer, of a specified question, that should win. This function is needed to avoid the \"stack too deep error\". TRUSTED.\r\n     *  @param _arbitrationRequest Arbitration request to compute it's winner.\r\n     *  @param _lastAnswerOrCommitmentID The last answer given, or its commitment ID if it was a commitment, to the question in the Realitio contract.\r\n     *  @param _lastBond The bond paid for the last answer to the question in the Realitio contract.\r\n     *  @param _lastAnswerer The last answerer to the question in the Realitio contract.\r\n     *  @param _isCommitment Whether the last answer to the question in the Realitio contract used commit or reveal or not. True if it did, false otherwise.\r\n     *  @return winner The computed winner.\r\n     */\r\n    function computeWinner(\r\n        ArbitrationRequest storage _arbitrationRequest,\r\n        bytes32 _lastAnswerOrCommitmentID,\r\n        uint256 _lastBond,\r\n        address _lastAnswerer,\r\n        bool _isCommitment\r\n    ) internal view returns (address winner) {\r\n        bytes32 lastAnswer;\r\n        bool isAnswered;\r\n        if (_lastBond == 0) {\r\n            // If the question hasn't been answered, nobody is ever right.\r\n            isAnswered = false;\r\n        } else if (_isCommitment) {\r\n            (uint32 revealTS, bool isRevealed, bytes32 revealedAnswer) = realitio.commitments(_lastAnswerOrCommitmentID);\r\n            if (isRevealed) {\r\n                lastAnswer = revealedAnswer;\r\n                isAnswered = true;\r\n            } else {\r\n                require(revealTS <= uint32(block.timestamp), \"Arbitration cannot be done until the last answerer has had time to reveal its commitment.\");\r\n                isAnswered = false;\r\n            }\r\n        } else {\r\n            lastAnswer = _lastAnswerOrCommitmentID;\r\n            isAnswered = true;\r\n        }\r\n\r\n        // Note that 0-1=type(uint256).max. However starting from Solidity 0.8.x this won't be the case. https://docs.soliditylang.org/en/v0.8.0/080-breaking-changes.html\r\n        return isAnswered && lastAnswer == bytes32(_arbitrationRequest.ruling - 1) ? _lastAnswerer : _arbitrationRequest.requester;\r\n    }\r\n}\r\n"
    },
    "contracts/Migrations.sol": {
      "content": "// SPDX-License-Identifier: MIT\r\n\r\n// Truffle migration contract, do not review.\r\n\r\npragma solidity >=0.7.0 <0.8.0;\r\n\r\ncontract Migrations {\r\n    address public owner;\r\n    uint256 public last_completed_migration;\r\n\r\n    constructor() {\r\n        owner = msg.sender;\r\n    }\r\n\r\n    modifier restricted() {\r\n        if (msg.sender == owner) _;\r\n    }\r\n\r\n    function setCompleted(uint256 completed) public restricted {\r\n        last_completed_migration = completed;\r\n    }\r\n\r\n    function upgrade(address new_address) public restricted {\r\n        Migrations upgraded = Migrations(new_address);\r\n        upgraded.setCompleted(last_completed_migration);\r\n    }\r\n}\r\n"
    }
  },
  "settings": {
    "optimizer": {
      "enabled": false,
      "runs": 200
    },
    "outputSelection": {
      "*": {
        "*": [
          "abi",
          "evm.bytecode",
          "evm.deployedBytecode",
          "evm.methodIdentifiers",
          "metadata",
          "devdoc",
          "userdoc",
          "storageLayout",
          "evm.gasEstimates"
        ],
        "": [
          "ast"
        ]
      }
    },
    "metadata": {
      "useLiteralContent": true
    }
  }
}